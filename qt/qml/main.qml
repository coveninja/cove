import QtQuick
import QtQuick.Window
import QtWebEngine
import QtWebChannel
import mpv

// Single Quick scene: mpv FBO at the back, transparent WebEngineView on top.
// They share one scene graph and the web layer background is transparent, so
// the web UI composites over the video (what Electron cannot do).
Window {
    id: win
    visible: true
    width: 1366
    height: 850
    title: "Cove"
    color: "black"

    MpvObject {
        id: mpv
        anchors.fill: parent

        // Identifier the JS side uses: channel.objects.mpv
        WebChannel.id: "mpv"

        Component.onCompleted: {
            if (typeof mpvTestFile !== "undefined" && mpvTestFile.length > 0)
                mpv.play(mpvTestFile)
        }
    }

    // Bridge: exposes mpv's slots/signals to the web layer. QtWebEngine injects
    // qt.webChannelTransport into the page once this channel is set on the view.
    WebChannel {
        id: coveChannel
        registeredObjects: [mpv]
    }

    Connections {
        target: mpv
        function onFullscreenRequested(fs) {
            if (fs) {
                win.showFullScreen();
            } else if (win.visibility === Window.FullScreen) {
                win.showNormal();
            }
        }
    }

    WebEngineView {
        id: web
        anchors.fill: parent
        backgroundColor: "transparent"
        webChannel: coveChannel
        url: launchUrl

        // Open window.open() links (e.g. JustWatch provider pages) in the
        // system browser rather than a new Qt window.
        onNewWindowRequested: function(request) {
            Qt.openUrlExternally(request.requestedUrl)
            request.action = WebEngineNewWindowRequest.IgnoreRequest
        }

        // Forward JS console output to the Qt process stdout so it's visible
        // in the terminal alongside Go backend logs.
        onJavaScriptConsoleMessage: function(level, message, lineNumber, sourceID) {
            var prefix = "[js] "
            if (level === 1) prefix = "[js:warn] "
            else if (level >= 2) prefix = "[js:err] "
            console.log(prefix + message)
        }
    }
}
